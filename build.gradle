allprojects {
    group 'me.kolek.fix'
    version '1.0.0-SNAPSHOT'

    apply plugin: 'maven'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url mavenRepoSnapshot
        }
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: version.endsWith("-SNAPSHOT") ? mavenRepoSnapshot : mavenRepoStaging) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    name name
                    description description
                    url 'https://github.com/ckolek/java-fix'
                    scm {
                        url 'https://github.com/ckolek/java-fix'
                        connection 'git@github.com:ckolek/java-fix.git'
                        developerConnection 'git@github.com:ckolek/java-fix.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'ckolek'
                            name 'Chris Kolek'
                            email 'christopher.w@kolek.me'
                        }
                    }
                }
            }
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: '19.0'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
}
