buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'me.kolek.gradle', name: 'gradle-coveralls-plugin', version: '1.0.0'
    }
}

allprojects {
    group 'me.kolek.fix'
    version '1.0.0-SNAPSHOT'

    apply plugin: 'maven'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url mavenRepoSnapshot
        }
    }

    ext."signing.keyId" = System.env.GPG_KEY_ID
    ext."signing.password" = System.env.GPG_PASSPHRASE
    ext."signing.secretKeyRingFile" = "$rootProject.projectDir/cd/codesigning.gpg"

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: mavenRepoStaging) {
                    authentication(userName: "$System.env.OSSRH_JIRA_USERNAME", password: "$System.env.OSSRH_JIRA_PASSWORD")
                }

                snapshotRepository(url: mavenRepoSnapshot) {
                    authentication(userName: "$System.env.OSSRH_JIRA_USERNAME", password: "$System.env.OSSRH_JIRA_PASSWORD")
                }

                pom.project {
                    name name
                    description description
                    url 'https://github.com/ckolek/java-fix'
                    scm {
                        url 'https://github.com/ckolek/java-fix'
                        connection 'git@github.com:ckolek/java-fix.git'
                        developerConnection 'git@github.com:ckolek/java-fix.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'ckolek'
                            name 'Chris Kolek'
                            email 'christopher.w@kolek.me'
                        }
                    }
                }
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'me.kolek.coveralls'

    sourceCompatibility = 1.8

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: '19.0'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    jacoco {
        toolVersion = "0.7.9"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }

    coveralls {
        repoToken System.env.COVERALLS_REPO_TOKEN
        services {
            travis()
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
}
